#!/usr/bin/env fish

function log -a message
    set_color blue
    echo -n "==> "
    set_color normal
    echo "$message"
end

function confirm -a prompt default
    # Helper function for yes/no prompts
    # Usage: confirm "message" "Y" (default yes) or confirm "message" "N" (default no)
    read -P "$prompt " answer
    set -l answer (string lower $answer)

    if test -z "$answer"
        test "$default" = Y
    else
        string match -q -r '^y' $answer
    end
end

log "provisioning macOS environment for $USER"

# Check we're on macOS
test (uname) != Darwin
and echo 'error: this script is for macOS only'
and exit 1

# Install all packages
log "installing packages"
brew bundle
or exit 1

# Sync all configurations first (get configs in place)
./sync
or exit 1

# Set up shell
log "setting up shell"
fish_add_path /opt/homebrew/bin

if test $SHELL != /opt/homebrew/bin/fish
    sudo chsh -s /opt/homebrew/bin/fish $USER
    or exit 1
end

./scripts/fish_generate_completions.fish
or echo 'warning: failed to generate fish completions'

# Suppress login messages
touch ~/.hushlogin

# Set up terminal tools
log "setting up terminal tools"
if not test -d ~/.config/tmux/plugins/tpm
    git clone https://github.com/tmux-plugins/tpm ~/.config/tmux/plugins/tpm >/dev/null 2>&1
    or echo 'warning: failed to install tmux plugin manager'
end

if test -d ~/.config/tmux/plugins/tpm
    ~/.config/tmux/plugins/tpm/bin/install_plugins
    or echo 'warning: failed to install tmux plugins'
end

# Initialize git-lfs (sets up git hooks)
if command -v git >/dev/null 2>&1
    and git lfs version >/dev/null 2>&1
    git lfs install
    or echo 'warning: failed to initialize git-lfs'
end

# Set up Docker CLI plugins (compose, buildx)
./scripts/provision_docker.fish
or echo 'warning: failed to set up docker'

# Set up desktop apps
log "setting up desktop apps"
if confirm "Would you like to delete old login items? [y/N]" N
    osascript -e 'tell application "System Events" to delete every login item'
end

if not osascript -e 'tell application "System Events" to get the name of every login item' | grep -q WezTerm
    osascript -e 'tell application "System Events" to make login item at end with properties {path:"/Applications/WezTerm.app", hidden:false}' 2>/dev/null
end

if not osascript -e 'tell application "System Events" to get the name of every login item' | grep -q Raycast
    osascript -e 'tell application "System Events" to make login item at end with properties {path:"/Applications/Raycast.app", hidden:false}' 2>/dev/null
end

# Set up window management
log "setting up window management"
yabai --install-service
or echo 'warning: failed to install yabai service'

defaults write com.apple.dock mru-spaces -bool false

yabai --start-service
or echo 'warning: failed to start yabai service'

if confirm "Set up yabai scripting addition? (requires SIP disabled + boot-args configured) [y/N]" N
    ./scripts/yabai_setup_sa.fish
    or echo 'warning: yabai scripting addition setup failed'
end

skhd --install-service
or echo 'warning: failed to install skhd service'

./scripts/skhd_fix_performance.fish >/dev/null 2>&1

skhd --stop-service
skhd --start-service
or echo 'warning: failed to start skhd service'

# Set up VSCode
log "setting up vscode"
./scripts/vscode_install_extensions.fish
or echo 'warning: failed to install vscode extensions'

# Set macOS defaults
if confirm "Set macOS system defaults? [y/N]" N
    ./scripts/macos_set_defaults.fish
    or echo 'warning: failed to set macOS defaults'
end

echo 'done provisioning macOS'
