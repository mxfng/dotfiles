#!/usr/bin/env fish

function log -a message
    set_color blue
    echo -n "==> "
    set_color normal
    echo "$message"
end

log 'syncing configurations'

# Ensure directories exist
mkdir -p ~/.config ~/Developer/scripts

# Sync terminal configs
log 'syncing terminal configs'
rsync -aP \
    ./.config/fish \
    ./.config/nvim \
    ./.config/tmux \
    ~/.config/
or exit 1

rsync -aP ./scripts/ ~/Developer/scripts/
or exit 1

rsync -a \
    ./.config/starship.toml \
    ./.tool-versions \
    ./.hushlogin \
    ~/
or exit 1

# Sync desktop configs
log 'syncing desktop configs'
rsync -aP ./.config/wezterm ~/.config/
or exit 1

rsync -aP ./.config/skhd ~/.config/
or exit 1

# Sync git config (preserve user info)
log 'syncing git config'
set -l current_name (git config --global user.name 2>/dev/null)
set -l current_email (git config --global user.email 2>/dev/null)

rsync -aP ./.config/git ~/.config/
or exit 1

# Restore or prompt for git user info
if test -z "$current_name" -o -z "$current_email"
    echo 'Please set your git user:'

    if test -z "$current_name"
        read -P 'name: ' name
        git config --global user.name $name
    else
        echo "name: $current_name (already set)"
    end

    if test -z "$current_email"
        read -P 'email: ' email
        git config --global user.email $email
    else
        echo "email: $current_email (already set)"
    end
else
    echo "Git user already configured: $current_name <$current_email>"
    git config --global user.name $current_name
    git config --global user.email $current_email
end

# Set fish universal variables
log 'setting fish universal variables'
source ./scripts/fish_set_universal_vars.fish
or exit 1

echo 'done syncing.'
