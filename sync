#!/usr/bin/env fish

function log -a message
    set_color blue
    echo -n "==> "
    set_color normal
    echo "$message"
end

log 'syncing dotfiles'

# Ensure directories exist
mkdir -p ~/.config ~/Developer/scripts

# Capture git user info before syncing (will restore after)
set -l git_name (git config --global user.name 2>/dev/null)
set -l git_email (git config --global user.email 2>/dev/null)

# Sync XDG configs
rsync -aP --delete \
    --exclude fish/completions/ \
    --exclude fish/fish_variables \
    --exclude tmux/plugins/ \
    ./.config/fish \
    ./.config/git \
    ./.config/nvim \
    ./.config/skhd \
    ./.config/starship.toml \
    ./.config/tmux \
    ./.config/wezterm \
    ./.config/yabai \
    ~/.config/
or exit 1

rsync -aP --delete ./scripts/ ~/Developer/scripts/
or exit 1

# Sync non-XDG configs
if test -d ./.config/code
    rsync -a ./.config/code/settings.json "/Users/$USER/Library/Application Support/Code/User/" 2>/dev/null
    rsync -a ./.config/code/keybindings.json "/Users/$USER/Library/Application Support/Code/User/" 2>/dev/null
end

# Sync AI agent contexts
./scripts/sync_ai_agents.fish

log 'syncing git config'

if test -z "$git_name" -o -z "$git_email"
    echo 'please set your git user:'

    if test -z "$git_name"
        read -P 'name: ' name
        git config --global user.name $name
    else
        echo "name: $git_name (already set)"
    end

    if test -z "$git_email"
        read -P 'email: ' email
        git config --global user.email $email
    else
        echo "email: $git_email (already set)"
    end
else
    echo "git user: $git_name <$git_email>"
    git config --global user.name $git_name
    git config --global user.email $git_email
end

echo 'done syncing'
